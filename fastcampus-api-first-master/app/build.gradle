import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
buildscript {

    dependencies {
        classpath project(':masilcodegen')
    }
}
plugins {
    id 'java'
    id 'org.springframework.boot' version "2.5.0"
    id 'io.spring.dependency-management' version "1.0.11.RELEASE"

    id "org.openapi.generator" version "5.1.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //implementation 'org.openapitools:openapi-generator:5.1.0'
    //fixed for swagger
    //compileOnly 'io.swagger:swagger-annotations:1.6.1' //see templates
    compileOnly 'jakarta.validation:jakarta.validation-api:2.0.2'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.1.0'
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

openApiValidate {
    inputSpec="$projectDir/src/main/swagger/api.yml"
    recommend=true
}

openApiGenerate {
    inputSpec="$projectDir/src/main/swagger/api.yml"
    generatorName = "masil"
    configFile= "$projectDir/src/main/swagger/config.json"
    outputDir= "$projectDir/src/main/swagger/.generated"
}
tasks.named("openApiGenerate") {
    doFirst {
        delete openApiGenerate.outputDir.get()
    }
}
task openApiHtmlGenerate(type: GenerateTask){
    inputSpec="$projectDir/src/main/swagger/api.yml"
    generatorName = "html2"
    outputDir= "$projectDir/src/main/swagger/.generated/src/main/resources/swagger/swagger-html"
}

task openApiYmlGenerate(type: GenerateTask){
    inputSpec="$projectDir/src/main/swagger/api.yml"
    generatorName = "openapi-yaml"
    outputDir= "$projectDir/src/main/swagger/.generated/src/main/resources/swagger/"
    configOptions = [
            outputFile: "openapi.yml"
    ]
}

task openApiJsonGenerate(type: GenerateTask){
    inputSpec="$projectDir/src/main/swagger/api.yml"
    generatorName = "openapi"
    outputDir= "$projectDir/src/main/swagger/.generated/src/main/resources/swagger/"

    doLast {
        copy {
            from("$projectDir/src/main/swagger/redoc")
            into("$projectDir/src/main/swagger/.generated/src/main/resources/swagger/redoc")
        }
        copy {
            from("$projectDir/src/main/swagger/ui")
            into("$projectDir/src/main/swagger/.generated/src/main/resources/swagger/ui")
        }
    }
}

tasks.openApiGenerate.dependsOn  tasks.openApiValidate
tasks.compileJava.dependsOn tasks.openApiGenerate, tasks.openApiHtmlGenerate, tasks.openApiYmlGenerate, tasks.openApiJsonGenerate
sourceSets.main.java.srcDir "${openApiGenerate.outputDir.get()}/src/main/java"
sourceSets.main.resources.srcDir "${openApiGenerate.outputDir.get()}/src/main/resources"